module example.simple

validator OnlyElectronJSDotOrg = {
    production: AND(
        origin is "https://www.electronjs.org"
        OR(
            is_main_frame is true
            protocol is "https"
        )
    )
    development: AND(
        origin is "https://dev.electronjs.org"
        OR(
            is_main_frame is true
            protocol is "https"
        )
    )
}

validator OnlyCustomProtocol = AND(
    origin is "myapp://main"
    is_main_frame is true
)

// Validator using startsWith for prefix matching
validator AdminPagesOnly = AND(
    is_main_frame is true
    href startsWith "https://myapp.com/admin"
)

// Validator using endsWith for suffix matching
validator InternalDomainsOnly = AND(
    is_main_frame is true
    hostname endsWith ".example.com"
)

validator OnlyWithDynamicGlobal = AND(
    dynamic_global(fooBarRabbitCheese)
    is_main_frame is true
)

// Validator demonstrating URL-based checks with multiple conditions
// Invalid URLs (like about:blank) are handled gracefully by returning false
validator MultiOriginValidator = {
    production: AND(
        is_main_frame is true
        OR(
            origin is "https://example.com"
            hostname is "localhost"
            protocol is "file:"
        )
    )
    development: AND(
        is_main_frame is true
        OR(
            origin is "https://dev.example.com"
            hostname is "localhost"
            protocol is "file:"
        )
    )
}

subtype ComputerName = string(
    minLength: 2
    maxLength: 20
)

enum ComputerPlatform {
    MacOSX = "darwin"
    Windows
    Linux
}

structure ComputerStuff {
    name: string
    platform?: ComputerPlatform
    gpuInfo: {
        name: string
        year: number
        enabled: boolean
    }
}

// Demonstrates using "type" as a property name (previously a reserved word bug)
structure Message {
    type: string
    payload: string
}

// Demonstrates store with structure return type (tests import generation fix)
structure ConnectionState {
    connected: boolean
    latency?: number
}

// Demonstrates using "type" as a method parameter name
structure MessageResult {
    success: boolean
}

[RendererAPI]
[Validator=OnlyElectronJSDotOrg]
[ContextBridge]
[LegacyAlias=OldComputerInfo]
interface ComputerInfo {
    GetName(foo: ComputerPlatform) -> string?
    [Sync]
    GetNameSync() -> string
    GetStuff() -> ComputerStuff
    GetOptionalNumber() -> number?
    [NotImplemented]
    GetFutureFeature() -> string
    [Event]
    shutdown(foo: string)
    [Store]
    blah() -> string
    // Demonstrates store with structure return type (tests import generation fix)
    [Store]
    connection() -> ConnectionState
    // Demonstrates using "type" as a method parameter name (previously a reserved word bug)
    SendMessage(type: string, payload: string) -> MessageResult
}

// Demonstrates that interfaces without stores are still exported from the renderer
// (Previously a bug where renderer exports were only generated for interfaces with stores)
[RendererAPI]
[Validator=OnlyCustomProtocol]
[ContextBridge]
interface CustomProtocolAPI {
    GetSecureData() -> string
}
